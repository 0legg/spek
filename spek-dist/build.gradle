description = 'Spek Distribution packages'

apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'


mainClassName = 'org.spek.console.ConsolePackage'

dependencies {
    compile project(':spek-core')
}


distZip {
    archiveName = 'console-runner.zip'
}


jar {
    println "Building version: " + version
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}


task sourceJar(type: Jar) {
    from project(':spek-core').sourceSets.main.allSource
}

publishing {
   publications {
       mavenJava(MavenPublication) {
          groupId = "org.spek"
          artifactId = "spek"
          version = "0.1-SNAPSHOT-${rootProject.version}"
         
          from project(':spek-core').components.java


          artifact sourceJar {
                classifier "sources"
          }
       }
   }


   repositories {
       maven {
            url "http://repository.jetbrains.com/spek"
            if (rootProject.hasProperty("deploy_username") && rootProject.hasProperty("deploy_password")) { 
               credentials { 
                 username "${rootProject.deploy_username}"
                 password "${rootProject.deploy_password}"
               }
            }
       }
   }

}

